apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/GlitchTech/GlitchTech-Common'      // Homepage URL of the library
def gitUrl = 'https://github.com/GlitchTech/GlitchTech-Common.git'   // Git repository URL
group = "com.glitchtechscience.gts-common"
version = "1.9.3"

android {
	compileSdkVersion 28
	buildToolsVersion '28.0.3'
	resourcePrefix "gtscommon__"

	dataBinding {
		enabled = true
	}

	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 28
		versionCode 100
		versionName version
	}

	buildTypes {
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	//** Google Support Libraries
	implementation 'com.android.support:support-v4:28.0.0'
	implementation 'com.android.support:appcompat-v7:28.0.0'
	implementation 'com.android.support:design:28.0.0'
	implementation 'com.android.support:recyclerview-v7:28.0.0'

	// https://github.com/futuresimple/android-floating-action-button
	implementation 'com.getbase:floatingactionbutton:1.10.1'

	// https://github.com/daimajia/AndroidViewAnimations
	implementation 'com.nineoldandroids:library:2.4.0'
	implementation 'com.daimajia.easing:library:1.0.1@aar'
	implementation 'com.daimajia.androidanimations:library:1.1.3@aar'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")

	configurations = ['archives']

	pkg {
		repo = "maven"
		name = "gts-common"

		websiteUrl = siteUrl
		vcsUrl = gitUrl

		licenses = ["Apache-2.0"]

		publish = true
	}
}

install {
	repositories.mavenInstaller {
		// This generates POM.xml with proper parameters
		pom {
			//noinspection GroovyAssignabilityCheck
			project {
				packaging 'aar'

				// Add your description here
				name 'This is a common use library for GlitchTech Science projects.'
				url siteUrl

				// Set your license
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'glitchtechscience'
						name = 'GlitchTech Science'
						email = 'glitchtechscience@gmail.com'
					}
				}
				scm {
					connection gitUrl
					developerConnection gitUrl
					url siteUrl
				}
			}
		}
	}
}

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
	// options.encoding = 'UTF-8'
}

artifacts {
	archives javadocJar
	archives sourcesJar
}
