apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.7.0"

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.2"
	resourcePrefix "gtscommon__"

	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 23
		versionCode 100
		versionName version
	}

	buildTypes {
	}
}

def siteUrl = 'https://github.com/GlitchTech/GlitchTech-Common'      // Homepage URL of the library
def gitUrl = 'https://github.com/GlitchTech/GlitchTech-Common.git'   // Git repository URL
group = "com.glitchtechscience.gts-common"

install {
	repositories.mavenInstaller {
		// This generates POM.xml with proper parameters
		pom {
			//noinspection GroovyAssignabilityCheck
			project {
				packaging 'aar'

				// Add your description here
				name 'This is a common use library for GlitchTech Science projects.'
				url siteUrl

				// Set your license
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'glitchtechscience'
						name = 'GlitchTech Science'
						email = 'glitchtechscience@gmail.com'
					}
				}
				scm {
					connection gitUrl
					developerConnection gitUrl
					url siteUrl

				}
			}
		}
	}
}

dependencies {
	compile fileTree( dir: 'libs', include: ['*.jar'] )

	//** Google Support Libraries
	compile 'com.android.support:support-v4:23.3.0'
	compile 'com.android.support:appcompat-v7:23.3.0'
	compile 'com.android.support:recyclerview-v7:23.3.0'
	compile 'com.android.support:design:23.3.0'

	// https://github.com/futuresimple/android-floating-action-button
	compile 'com.getbase:floatingactionbutton:1.2.0'

	// https://github.com/daimajia/AndroidViewAnimations
	compile 'com.nineoldandroids:library:2.4.0'
	compile 'com.daimajia.easing:library:1.0.1@aar'
	compile 'com.daimajia.androidanimations:library:1.1.3@aar'
}

task sourcesJar( type: Jar ) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc( type: Javadoc ) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files( android.getBootClasspath().join( File.pathSeparator ) )
	failOnError false
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
	classifier = 'javadoc'
	from javadoc.destinationDir
	// options.encoding = 'UTF-8'
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

Properties properties = new Properties()
properties.load( project.rootProject.file( 'local.properties' ).newDataInputStream() )

bintray {
	user = properties.getProperty( "bintray.user" )
	key = properties.getProperty( "bintray.apikey" )

	configurations = ['archives']
	pkg {
		repo = "maven"
		name = "gts-common"
		websiteUrl = siteUrl
		vcsUrl = gitUrl
		licenses = ["Apache-2.0"]
		publish = true
	}
}
